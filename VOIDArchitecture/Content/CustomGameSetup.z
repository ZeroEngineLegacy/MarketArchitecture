class CustomGameSetup : ZilchComponent
{
    //Archetype to be used to create the initial space for the game
    [Property]
    var DefaultSpaceArchetype : Archetype;
    
    //Level to load into DefaultSpace
    [Property]
    var DefaultLevel : Level;
    
    //The initial space created by this component
    var DefaultSpace : Space = null;
    
    function Initialize(init : CogInitializer)
    {
        //Connect to GameSetup
        //This is what allows us to hijack the creation of the initial space.
        Zero.Connect(this.Owner, Events.GameSetup, this.OnGameSetup);
    }

    function OnGameSetup(event : GameEvent)
    {
        //Handle the event so no other components use it
        event.Handled = true;
        
        //Create the initial space for the application
        this.DefaultSpace = event.Game.CreateNamedSpace(this.DefaultSpaceArchetype.Name, this.DefaultSpaceArchetype);
        
        //Load the default level
        this.DefaultSpace.LoadLevel(this.DefaultLevel);
    }
}
